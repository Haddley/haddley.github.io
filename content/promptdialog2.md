---
title: "Business Central (Part 28) PrompDialog 2"
description: "A comprehensive guide covering business central (part 28) prompdialog 2"
date: "2025-09-20"
category: "Business Central"
image: "/assets/images/promptdialog2/hero.png"
tags: ["azure","cloud","ai","business central","dynamics"]
---

![](/assets/images/promptdialog2/dynamics365-color.svg)
*By Microsoft, Public Domain*

## Prompt Dialog (Part 2)

Copilot Prompt Interface: A user interface (UI) component designed to collect user inputs and queries for Microsoft Dynamics 365 Business Central's Copilot feature. This dialog page enables users to interact with the AI assistant by submitting natural language prompts or requests.

AI Integration Module: A backend system component that acts as the intermediary between Business Central and Azure OpenAI Service. This module handles API communication, data processing, and secure routing of user prompts to Azure's AI services, while managing responses back to the Business Central application.

Azure OpenAI Resource: A cloud-based service within the Microsoft Azure ecosystem that provides access to advanced OpenAI language models (e.g., GPT-4). This resource enables AI-driven capabilities such as natural language understanding, content generation, and contextual analysis, which power Copilot's intelligent features in Business Central.

![](/assets/images/promptdialog2/screenshot202025-05-2320at209.29.26e280afpm-2136x1241.png)
*I updated the LLM Prompt Dialog page*

![](/assets/images/promptdialog2/screenshot202025-05-2320at209.29.40e280afpm-2136x1253.png)
*Output is generated by a Large Language Model and returned to the user*

![](/assets/images/promptdialog2/screenshot202025-05-2320at209.42.43e280afpm-2136x1242.png)
*I updated the RunGeneration procedure*

![](/assets/images/promptdialog2/screenshot202025-05-2320at209.51.18e280afpm-2136x1114.png)

*For more detail please see* [this blog page](/posts/azureopenal/).

![](/assets/images/promptdialog2/screenshot202025-05-2320at2010.12.31e280afpm-2136x962.png)
*I updated the "publisher" property*

![](/assets/images/promptdialog2/screenshot202025-05-2320at2010.14.21e280afpm-2136x646.png)
*There is a Copilot & agent capabilities page in Business Central*

![](/assets/images/promptdialog2/screenshot202025-05-2320at2010.18.05e280afpm-2136x1171.png)
*The LLM Capability has been added to the list*


## Pag50100.LLM.al updated

```text
namespace LLM.LLM;

using System.AI;

enumextension 50100 LLM extends "Copilot Capability"
{
    value(50100; "LLM") { }
}

page 50100 LLM
{
    ApplicationArea = All;
    Caption = 'LLM';
    PageType = PromptDialog;
    Extensible = false;
    DataCaptionExpression = Prompt;
    IsPreview = true;

    layout
    {
        #region input section
        area(Prompt)
        {
            field(PromptField; Prompt)
            {
                ShowCaption = false;
                MultiLine = true;
                InstructionalText = 'Message Large Language Model';
            }
        }
        #endregion

        #region output section
        area(Content)
        {
            field(OutputFiled; Output)
            {
                ShowCaption = false;
                MultiLine = true;
            }
        }
        #endregion
    }
    actions
    {
        #region prompt guide
        area(PromptGuide)
        {
            action("Capital City")
            {
                ApplicationArea = All;
                Caption = 'Capital City';
                ToolTip = 'What is the Capital of ...';

                trigger OnAction()
                begin
                    Prompt := 'What is the Capital of <Country>?';
                end;
            }
        }
        #endregion

        #region system actions
        area(SystemActions)
        {
            systemaction(Generate)
            {
                Caption = 'Generate';
                ToolTip = 'Generate a response.';

                trigger OnAction()
                begin
                    RunGeneration();
                end;
            }
            systemaction(OK)
            {
                Caption = 'OK';
                ToolTip = 'Save the result.';
            }
            systemaction(Cancel)
            {
                Caption = 'Cancel';
                ToolTip = 'Discard the result.';
            }
            systemaction(Regenerate)
            {
                Caption = 'Regenerate';
                ToolTip = 'Regenerate a response.';

                trigger OnAction()
                begin
                    RunGeneration();
                end;
            }
            #endregion
        }
    }
    var
        Prompt: Text;
        Output: Text;


    local procedure RunGeneration()
    var
        CopilotCapability: Codeunit "Copilot Capability";
        AzureOpenAI: Codeunit "Azure OpenAI";
        AOAIDeployments: Codeunit "AOAI Deployments";
        AOAIChatCompletionParams: Codeunit "AOAI Chat Completion Params";
        AOAIChatMessages: Codeunit "AOAI Chat Messages";
        AOAIOperationResponse: Codeunit "AOAI Operation Response";
        UserMessage: TextBuilder;
    begin

        if not CopilotCapability.IsCapabilityRegistered(Enum::"Copilot Capability"::LLM) then
            CopilotCapability.RegisterCapability(Enum::"Copilot Capability"::LLM, 'https://about:none');

        AzureOpenAI.SetAuthorization(Enum::"AOAI Model Type"::"Chat Completions", GetEndpoint(), GetDeployment(), GetApiKey());
        AzureOpenAI.SetCopilotCapability(Enum::"Copilot Capability"::LLM);

        AOAIChatCompletionParams.SetMaxTokens(2500);
        AOAIChatCompletionParams.SetTemperature(0);
        AOAIChatMessages.AddUserMessage(Prompt);

        AzureOpenAI.GenerateChatCompletion(AOAIChatMessages, AOAIChatCompletionParams, AOAIOperationResponse);
        if (AOAIOperationResponse.IsSuccess()) then
            Output := AOAIChatMessages.GetLastMessage()
        else
            Output := ('Error: ' + AOAIOperationResponse.GetError());

    end;


    local procedure GetEndpoint(): Text
    var
    begin
        exit('https://haddley-ai.openai.azure.com');
    end;

    local procedure GetDeployment(): Text
    var
    begin
        exit('gpt-4o');
    end;

    [NonDebuggable]
    local procedure GetApiKey(): Text //SecretText
    var
    begin
        exit('12345678901234567890123456789012');
    end;



}
```